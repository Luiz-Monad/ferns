
SET(VERSION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/src/general.h")
file(STRINGS "${VERSION_FILE}" FRN_VERSION_PARTS REGEX "#define FRN_VERSION_[A-Z]+[ ]+" )

string(REGEX REPLACE ".+FRN_VERSION_MAJOR[ ]+([0-9]+).*" "\\1" FRN_VERSION_MAJOR "${FRN_VERSION_PARTS}")
string(REGEX REPLACE ".+FRN_VERSION_MINOR[ ]+([0-9]+).*" "\\1" FRN_VERSION_MINOR "${FRN_VERSION_PARTS}")
string(REGEX REPLACE ".+FRN_VERSION_REVISION[ ]+([0-9]+).*" "\\1" FRN_VERSION_PATCH "${FRN_VERSION_PARTS}")
string(REGEX REPLACE ".+FRN_VERSION_STATUS[ ]+\"([^\"]*)\".*" "\\1" FRN_VERSION_STATUS "${FRN_VERSION_PARTS}")

set(FRN_VERSION_PLAIN "${FRN_VERSION_MAJOR}.${FRN_VERSION_MINOR}.${FRN_VERSION_PATCH}")
set(FRN_VERSION "${FRN_VERSION_PLAIN}${FRN_VERSION_STATUS}")
set(FRN_SOVERSION "${FRN_VERSION_MAJOR}.${FRN_VERSION_MINOR}")
set(FRN_LIBVERSION "${FRN_VERSION_MAJOR}.${FRN_VERSION_MINOR}.${FRN_VERSION_PATCH}")

# create a dependency on the version file
# we never use the output of the following command but cmake will rerun automatically if the version file changes
configure_file("${VERSION_FILE}" "${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/_junk/version.junk" COPYONLY)
